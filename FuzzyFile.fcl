FUNCTION_BLOCK fillRatio

// Define input variables
VAR_INPUT				
	prefill : REAL;
	velocity : REAL;
	probability : REAL;
END_VAR

// Define output variable
VAR_OUTPUT				
	fill : REAL;
END_VAR


FUZZIFY prefill			
	TERM low := (0, 1) (0.1, 1) (0.3,0);
	TERM medium := (0.25,0) (0.45,1) (0.55,1) (0.75,0);
	TERM high := (0.7,0) (0.8,1) (1,1);
END_FUZZIFY


FUZZIFY velocity			
	TERM low := (0, 1) (1,1) (3,0) ;
	TERM medium := (2.5,0) (4.5,1) (5.5,1) (7.5,0);
	TERM high := (7,0) (9,1) (10,1);
END_FUZZIFY

FUZZIFY probability
	TERM low := (0, 1) (0.1 , 1) (0.3,0);
	TERM medium := (0.25,0) (0.45,1) (0.55,1) (0.75,0);
	TERM high := (0.7,0) (0.8,1) (1,1);
END_FUZZIFY

DEFUZZIFY fill			
	TERM low := (0, 1) (0.1 , 1) (0.35,0);
	TERM medium := (0.35,0) (0.5,1) (0.65,1) (0.8,0);
	TERM high := (0.75,0) (0.85,1) (1,1);
		
	METHOD : COG;		// Use 'Center Of Gravity' defuzzification method
	DEFAULT := 0;		// Default value is 0 (if no rule activates defuzzifier)
END_DEFUZZIFY

// Inference rules
RULEBLOCK No1
	AND : MIN;	// Use 'min' for 'and'
	ACT : MIN;	// Use 'min' activation method
	ACCU : MAX;	// Use 'max' accumulation method
	
	//rule definition
	RULE 1 : IF prefill IS low THEN fill IS low;
	RULE 2 : IF prefill IS medium AND probability IS low AND velocity IS low THEN fill IS low;
	RULE 3 : IF prefill IS medium AND probability IS low AND velocity IS medium THEN fill IS low;
	RULE 4 : IF prefill IS medium AND probability IS low AND velocity IS high THEN fill IS low;
	
	RULE 5 : IF prefill IS medium AND probability IS medium AND velocity IS low THEN fill IS low;
	RULE 6 : IF prefill IS medium AND probability IS medium AND velocity IS medium THEN fill IS medium;
	RULE 7 : IF prefill IS medium AND probability IS medium AND velocity IS high THEN fill IS medium;
	
	RULE 8 : IF prefill IS medium AND probability IS high AND velocity IS low THEN fill IS low;
	RULE 9 : IF prefill IS medium AND probability IS high AND velocity IS medium THEN fill IS medium;
	RULE 10 : IF prefill IS medium AND probability IS high AND velocity IS high THEN fill IS medium;
	
	RULE 11 : IF prefill IS high AND probability IS low AND velocity IS low THEN fill IS low;
	RULE 12 : IF prefill IS high AND probability IS low AND velocity IS medium THEN fill IS low;
	RULE 13 : IF prefill IS high AND probability IS low AND velocity IS high THEN fill IS low;
	
	RULE 14 : IF prefill IS high AND probability IS medium AND velocity IS low THEN fill IS low;
	RULE 15 : IF prefill IS high AND probability IS medium AND velocity IS medium THEN fill IS medium;
	RULE 16 : IF prefill IS high AND probability IS medium AND velocity IS high THEN fill IS medium;
	
	RULE 17 : IF prefill IS high AND probability IS high AND velocity IS low THEN fill IS low;
	RULE 18 : IF prefill IS high AND probability IS high AND velocity IS medium THEN fill IS medium;
	RULE 19 : IF prefill IS high AND probability IS high AND velocity IS high THEN fill IS high;
	
END_RULEBLOCK

END_FUNCTION_BLOCK